# Maintainer: Gioacchino Mazzurco <gio@altermundi.net>
# Contributor: AsamK
# Contributor: sehraf
# Contributor: stqn
# Contributor: JHeaton <jheaton at archlinux dot us>
# Contributor: Tristero <tristero at online dot de>
# Contributor: funkyou

pkgname=retroshare-friendserver-unstable
pkgver=unstable
pkgrel=20231018
pkgdesc="RetroShare anonymous trusted nodes network bootstrap service."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://retroshare.cc/"
license=('AGPL' 'GPL' 'LGPL')
depends=('bzip2' 'libupnp' 'libzip' 'openssl' 'rapidjson' 'sqlcipher')
makedepends=('cmake' 'doxygen' 'pkgconf' 'qt5-tools')
provides=('retroshare-friendserver')
conflicts=('retroshare-friendserver')

source=(RetroShare.tar.gz)
md5sums=(SKIP)

prepare() {
	cd "${srcdir}/RetroShare"
}

build() {
	[ "$(doxygen --version)" == "1.8.16" ] && {
		echo Doxygen 1.8.16 is not supported due to \
			https://github.com/doxygen/doxygen/issues/7236 please report it \
			to your distribution
		exit -1
	}

	qmake-qt5 --version
	gcc --version

	cd "${srcdir}/RetroShare"
	qmake-qt5 PREFIX=/usr RS_DATA_DIR=/usr/share/retroshare \
	$(build_scripts/OBS/get_source_version.sh) RS_MINI_VERSION=9999 \
	CONFIG-=debug CONFIG+=release CONFIG+=ipv6 \
	CONFIG+=no_retroshare_plugins CONFIG+=no_retroshare_gui CONFIG+=no_tests \
	CONFIG+=no_retroshare_service CONFIG+=retroshare_friendserver \
	CONFIG+=no_rs_jsonapi CONFIG+=no_rs_deep_search \
	RetroShare.pro
	make -j$(nproc) || (make && (echo "Parallel build failed" ; exit -1))
}

package() {
	cd "${srcdir}/RetroShare"
	make INSTALL_ROOT="${pkgdir}" install
}
